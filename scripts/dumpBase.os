#!/usr/bin/env oscript

#Использовать ParserFileV8i
#Использовать v8runner

Перем ИмяБазы;
Перем ТипВыгрузки;
Перем ПутьНазначения;
Перем ВерсияПлатформы;

Процедура ВыполнитьСкрипт()
    
    Если АргументыКоманднойСтроки.Количество() < 4 Тогда
        Сообщить("Недостаточно параметров");
        Сообщить("Использование: dumpBase.os <имя_базы> <тип_выгрузки> <путь_назначения> <версия_платформы>");
        Сообщить("Типы выгрузки: cf (файл), sources (исходники)");
        ВызватьИсключение "Недостаточно параметров";
    КонецЕсли;
    
    ИмяБазы = АргументыКоманднойСтроки[0];
    ТипВыгрузки = АргументыКоманднойСтроки[1];
    ПутьНазначения = АргументыКоманднойСтроки[2];
    ВерсияПлатформы = АргументыКоманднойСтроки[3];
    
    Попытка
        Парсер = Новый ПарсерСпискаБаз();
        РезультатПоиска = Парсер.НайтиПоИмени(ИмяБазы);
        
        Если РезультатПоиска = Неопределено Тогда
            ВызватьИсключение "База не найдена: " + ИмяБазы;
        КонецЕсли;
        
        // Извлекаем путь из Connect.Structure.File и убираем кавычки
        ПутьКБазе = СтрЗаменить(РезультатПоиска.Connect.Structure.File, """", "");
        
        СтрокаСоединенияБазы = "/F""" + ПутьКБазе + """";
        Конфигуратор = Новый УправлениеКонфигуратором();
        Конфигуратор.УстановитьКонтекст(СтрокаСоединенияБазы, "", "");
        Конфигуратор.ИспользоватьВерсиюПлатформы(ВерсияПлатформы);
        
        Если ТипВыгрузки = "cf" Тогда
            Конфигуратор.ВыгрузитьКонфигурациюВФайл(ПутьНазначения);
            Сообщить("Выгружено в файл: " + ПутьНазначения);
        ИначеЕсли ТипВыгрузки = "sources" Тогда
            Конфигуратор.ВыгрузитьКонфигурациюВФайлы(ПутьНазначения);
            Сообщить("Выгружено в каталог: " + ПутьНазначения);
        Иначе
            ВызватьИсключение "Неизвестный тип выгрузки: " + ТипВыгрузки + ". Доступны: cf, sources";
        КонецЕсли;
    Исключение
        ТекстОшибки = ОписаниеОшибки();
        ВызватьИсключение ТекстОшибки;
    КонецПопытки;
    
КонецПроцедуры

ВыполнитьСкрипт();
