#!/usr/bin/env oscript

#Использовать ParserFileV8i
#Использовать v8runner

Перем ИмяБазы;
Перем ТипВыгрузки;
Перем ПутьНазначения;
Перем ВерсияПлатформы;

Процедура ВыполнитьСкрипт()
    
    Если АргументыКоманднойСтроки.Количество() < 4 Тогда
        Сообщить("Недостаточно параметров");
        Сообщить("Использование: dumpBase.os <имя_базы> <тип_выгрузки> <путь_назначения> <версия_платформы>");
        Сообщить("Типы выгрузки: cf (файл), sources (исходники)");
        ВызватьИсключение "Недостаточно параметров";
    КонецЕсли;
    
    ИндексИмениБазы = 0;
    ИндексТипВыгрузки = 1;
    ИндексПутьНазначения = 2;
    ИндексВерсияПлатформы = 3;

    ИмяБазы = АргументыКоманднойСтроки[ИндексИмениБазы];
    ТипВыгрузки = АргументыКоманднойСтроки[ИндексТипВыгрузки];
    ПутьНазначения = АргументыКоманднойСтроки[ИндексПутьНазначения];
    ВерсияПлатформы = АргументыКоманднойСтроки[ИндексВерсияПлатформы];
    
    Попытка
        Парсер = Новый ПарсерСпискаБаз();
        РезультатПоиска = Парсер.НайтиПоИмени(ИмяБазы);
        
        Если РезультатПоиска = Неопределено Тогда
            ВызватьИсключение "База не найдена: " + ИмяБазы;
        КонецЕсли;
        
        ПутьКБазе = СтрЗаменить(РезультатПоиска.Connect.Structure.File, """", "");
        
        СтрокаСоединенияБазы = "/F""" + ПутьКБазе + """";
        Конфигуратор = Новый УправлениеКонфигуратором();
        Конфигуратор.УстановитьКонтекст(СтрокаСоединенияБазы, "", "");
        Конфигуратор.ИспользоватьВерсиюПлатформы(ВерсияПлатформы);
        
        Если ТипВыгрузки = "cf" Тогда
            Конфигуратор.ВыгрузитьКонфигурациюВФайл(ПутьНазначения);
        ИначеЕсли ТипВыгрузки = "sources" Тогда
            Конфигуратор.ВыгрузитьКонфигурациюВФайлы(ПутьНазначения);
        Иначе
            ВызватьИсключение "Неизвестный тип выгрузки: " + ТипВыгрузки + ". Доступны: cf, sources";
        КонецЕсли;
    Исключение
        ТекстОшибки = ОписаниеОшибки();
        ВызватьИсключение ТекстОшибки;
    КонецПопытки;
    
КонецПроцедуры

ВыполнитьСкрипт();
