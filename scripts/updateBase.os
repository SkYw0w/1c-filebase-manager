#!/usr/bin/env oscript

#Использовать ParserFileV8i
#Использовать v8runner

Перем ИмяБазы;
Перем ТипИсточника;
Перем ПутьИсточника;
Перем ВерсияПлатформы;

Процедура ВыполнитьСкрипт()
    
    Если АргументыКоманднойСтроки.Количество() < 4 Тогда
        Сообщить("Недостаточно параметров");
        Сообщить("Использование: updateBase.os <имя_базы> <тип_источника> <путь_источника> <версия_платформы>");
        ВызватьИсключение "Недостаточно параметров";
    КонецЕсли;
    
    ИндексИмениБазы = 0;
    ИндексТипИсточника = 1;
    ИндексПутьИсточника = 2;
    ИндексВерсияПлатформы = 3;

    ИмяБазы = АргументыКоманднойСтроки[ИндексИмениБазы];
    ТипИсточника = АргументыКоманднойСтроки[ИндексТипИсточника];
    ПутьИсточника = АргументыКоманднойСтроки[ИндексПутьИсточника];
    ВерсияПлатформы = АргументыКоманднойСтроки[ИндексВерсияПлатформы];
    
    Попытка        
        Парсер = Новый ПарсерСпискаБаз();
        РезультатПоиска = Парсер.НайтиПоИмени(ИмяБазы);
        
        Если РезультатПоиска = Неопределено Тогда
            ВызватьИсключение "База не найдена: " + ИмяБазы;
        КонецЕсли;
        
        ПутьКБазе = СтрЗаменить(РезультатПоиска.Connect.Structure.File, """", "");
        
        СтрокаСоединенияБазы = "/F""" + ПутьКБазе + """";
        
        Конфигуратор = Новый УправлениеКонфигуратором();
        Конфигуратор.УстановитьКонтекст(СтрокаСоединенияБазы, "", "");
        Конфигуратор.ИспользоватьВерсиюПлатформы(ВерсияПлатформы);
        
        Если ТипИсточника = "cf" Тогда
            Конфигуратор.ЗагрузитьКонфигурациюИзФайла(ПутьИсточника);
        ИначеЕсли ТипИсточника = "sources" Тогда
            Конфигуратор.ЗагрузитьКонфигурациюИзФайлов(ПутьИсточника);
        Иначе
            ВызватьИсключение "Неизвестный тип источника: " + ТипИсточника;
        КонецЕсли;
        
        Конфигуратор.ОбновитьКонфигурациюБазыДанных();
    Исключение
        ТекстОшибки = ОписаниеОшибки();
        ВызватьИсключение ТекстОшибки;
    КонецПопытки;
    
КонецПроцедуры

ВыполнитьСкрипт();
