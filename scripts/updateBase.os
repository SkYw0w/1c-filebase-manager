#!/usr/bin/env oscript

#Использовать ParserFileV8i
#Использовать v8runner

Перем ИмяБазы;
Перем ТипИсточника;
Перем ПутьИсточника;
Перем ВерсияПлатформы;

Процедура ВыполнитьСкрипт()
    
    Если АргументыКоманднойСтроки.Количество() < 4 Тогда
        Сообщить("Недостаточно параметров");
        Сообщить("Использование: updateBase.os <имя_базы> <тип_источника> <путь_источника> <версия_платформы>");
        ВызватьИсключение "Недостаточно параметров";
    КонецЕсли;
    
    ИмяБазы = АргументыКоманднойСтроки[0];
    ТипИсточника = АргументыКоманднойСтроки[1];
    ПутьИсточника = АргументыКоманднойСтроки[2];
    ВерсияПлатформы = АргументыКоманднойСтроки[3];
    
    Попытка
        Сообщить("Начало обновления конфигурации");
        Сообщить("Параметры: ИмяБазы=" + ИмяБазы + ", ТипИсточника=" + ТипИсточника + ", ПутьИсточника=" + ПутьИсточника);
        
        Парсер = Новый ПарсерСпискаБаз();
        РезультатПоиска = Парсер.НайтиПоИмени(ИмяБазы);
        
        Если РезультатПоиска = Неопределено Тогда
            ВызватьИсключение "База не найдена: " + ИмяБазы;
        КонецЕсли;
        
        // Извлекаем путь из Connect.Structure.File и убираем кавычки
        ПутьКБазе = СтрЗаменить(РезультатПоиска.Connect.Structure.File, """", "");
        Сообщить("Путь к базе найден: " + ПутьКБазе);
        
        СтрокаСоединенияБазы = "/F""" + ПутьКБазе + """";
        Сообщить("Строка соединения: " + СтрокаСоединенияБазы);
        
        Конфигуратор = Новый УправлениеКонфигуратором();
        Конфигуратор.УстановитьКонтекст(СтрокаСоединенияБазы, "", "");
        Конфигуратор.ИспользоватьВерсиюПлатформы(ВерсияПлатформы);
        Сообщить("Конфигуратор инициализирован");
        
        Если ТипИсточника = "cf" Тогда
            Сообщить("Загрузка конфигурации из .cf файла: " + ПутьИсточника);
            Конфигуратор.ЗагрузитьКонфигурациюИзФайла(ПутьИсточника);
            Сообщить("Конфигурация загружена из .cf файла");
        ИначеЕсли ТипИсточника = "sources" Тогда
            Сообщить("Загрузка конфигурации из исходников: " + ПутьИсточника);
            Конфигуратор.ЗагрузитьКонфигурациюИзФайлов(ПутьИсточника);
            Сообщить("Конфигурация загружена из исходников");
        Иначе
            ВызватьИсключение "Неизвестный тип источника: " + ТипИсточника;
        КонецЕсли;
        
        Сообщить("Обновление конфигурации БД...");
        Конфигуратор.ОбновитьКонфигурациюБазыДанных();
        Сообщить("Конфигурация обновлена: " + ИмяБазы);
    Исключение
        ТекстОшибки = ОписаниеОшибки();
        ВызватьИсключение ТекстОшибки;
    КонецПопытки;
    
КонецПроцедуры

ВыполнитьСкрипт();
