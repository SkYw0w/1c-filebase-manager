#!/usr/bin/env oscript

#Использовать ParserFileV8i
#Использовать v8runner

Перем ИмяБазы;
Перем ТипИсточника;
Перем ПутьИсточника;
Перем ИмяРасширения;
Перем ВерсияПлатформы;

Процедура ВыполнитьСкрипт()
    
    Если АргументыКоманднойСтроки.Количество() < 5 Тогда
        Сообщить("Недостаточно параметров");
        Сообщить("Использование: attachExtension.os <имя_базы> <тип_источника> <путь_источника> <имя_расширения> <версия_платформы>");
        Сообщить("Типы источника: cfe (файл), sources (исходники)");
        ВызватьИсключение "Недостаточно параметров";
    КонецЕсли;
    
    ИндексИмениБазы = 0;
    ИндексТипИсточника = 1;
    ИндексПутьИсточника = 2;
    ИндексИмяРасширения = 3;
    ИндексВерсияПлатформы = 4;

    ИмяБазы = АргументыКоманднойСтроки[ИндексИмениБазы];
    ТипИсточника = АргументыКоманднойСтроки[ИндексТипИсточника];
    ПутьИсточника = АргументыКоманднойСтроки[ИндексПутьИсточника];
    ИмяРасширения = АргументыКоманднойСтроки[ИндексИмяРасширения];
    ВерсияПлатформы = АргументыКоманднойСтроки[ИндексВерсияПлатформы];
    
    Попытка         
        Парсер = Новый ПарсерСпискаБаз();
        РезультатПоиска = Парсер.НайтиПоИмени(ИмяБазы);
        
        Если РезультатПоиска = Неопределено Тогда
            ВызватьИсключение "База не найдена: " + ИмяБазы;
        КонецЕсли;
        
        ПутьКБазе = СтрЗаменить(РезультатПоиска.Connect.Structure.File, """", "");
        
        СтрокаСоединенияБазы = "/F""" + ПутьКБазе + """";
        
        Конфигуратор = Новый УправлениеКонфигуратором();
        Конфигуратор.УстановитьКонтекст(СтрокаСоединенияБазы, "", "");
        Конфигуратор.ИспользоватьВерсиюПлатформы(ВерсияПлатформы);
        
        Если ТипИсточника = "cfe" Тогда
            Конфигуратор.ЗагрузитьРасширениеИзФайла(ПутьИсточника, ИмяРасширения, Истина);
        ИначеЕсли ТипИсточника = "sources" Тогда
            Конфигуратор.ЗагрузитьРасширениеИзФайлов(ПутьИсточника, ИмяРасширения);
        Иначе
            ВызватьИсключение "Неизвестный тип источника: " + ТипИсточника + ". Доступны: cfe, sources";
        КонецЕсли;
        
        Конфигуратор.ОбновитьКонфигурациюБазыДанных();
    Исключение
        ТекстОшибки = ОписаниеОшибки();
        ВызватьИсключение ТекстОшибки;
    КонецПопытки;
    
КонецПроцедуры

ВыполнитьСкрипт();
